plugins {
  id 'java-library'
  id 'pmd'
  id 'maven-publish'
  id 'org.cyclonedx.bom' version '2.4.1'
}

group = 'uk.gov.hmcts.cp'
version = System.getProperty('ARTEFACT_VERSION') ?: '0.0.999'

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }
}

def githubActor = project.findProperty("github.actor") ?: System.getenv("GITHUB_ACTOR")
def githubToken = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
def githubRepo = System.getenv("GITHUB_REPOSITORY")

def azureADOArtifactRepository = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
def azureADOArtifactActor = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
def azureADOArtifactToken = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")

repositories {
  mavenLocal()
  mavenCentral()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/$githubRepo")
      credentials {
        username = githubActor
        password = githubToken
      }
    }
    maven {
      name = "AzureArtifacts"
      url = uri(azureADOArtifactRepository)
      credentials {
        username = azureADOArtifactActor
        password = azureADOArtifactToken
      }
    }
  }
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
}

ext {
  springBootVersion = "3.4.3"
  lombokVersion = "1.18.42"
  jacksonVersion = "2.20.0"
  jupiterVersion = "6.0.0"
}

dependencies {
  compileOnly "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
  testImplementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"


  // --- Lombok for main and test code ---
  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

  testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
  testImplementation "org.junit.platform:junit-platform-launcher:$jupiterVersion"

  // Project specific dependencies
  api "org.springframework.boot:spring-boot-starter-artemis:$springBootVersion"
  api "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
  api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
  api "io.swagger.parser.v3:swagger-parser:2.1.35"

  implementation "org.mapstruct:mapstruct:1.5.5.Final"
  annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
}

jar {
  exclude("uk/gov/hmcts/cp/audit/DummySpringApplication.class")
}